apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

if (!rootProject.optimizeForDevelopment) {
    apply from: '../gradle/plugins/checkstyle-android.gradle'
    apply from: '../gradle/plugins/findbugs-android.gradle'
}

if (rootProject.testCoverage) {
    apply plugin: 'jacoco'
}

repositories {
    jcenter()
}

//noinspection GroovyAssignabilityCheck
configurations.all {
    resolutionStrategy {
        force "com.android.support:support-annotations:${androidSupportLibraryVersion}"
    }
}

dependencies {
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    compile project(':k9mail-library')
    compile project(':plugins:HoloColorPicker')
    compile project(':plugins:openpgp-api-lib:openpgp-api')
    compile "com.squareup.okio:okio:${okioVersion}"
    compile 'commons-io:commons-io:2.4'
    compile 'org.jsoup:jsoup:1.11.2'
    compile 'de.cketti.library.changelog:ckchangelog:1.2.1'
    compile 'com.github.bumptech.glide:glide:3.6.1'
    compile 'com.splitwise:tokenautocomplete:2.0.7'
    compile 'de.cketti.safecontentresolver:safe-content-resolver-v14:0.9.0'
    compile 'com.github.amlcurran.showcaseview:library:5.4.1'
    compile 'com.squareup.moshi:moshi:1.2.0'
    compile "com.jakewharton.timber:timber:${timberVersion}"
    compile 'net.jcip:jcip-annotations:1.0'
    compile 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'
    // ViewModel and LiveData
    implementation 'android.arch.lifecycle:extensions:1.1.0'
    // alternatively, just ViewModel
    implementation 'android.arch.lifecycle:viewmodel:1.1.0'
    // alternatively, just LiveData
    implementation 'android.arch.lifecycle:livedata:1.1.0'
    annotationProcessor "android.arch.lifecycle:compiler:1.1.0"
    // Room (use 1.1.0-alpha1 for latest alpha)
    implementation 'android.arch.persistence.room:runtime:1.0.0'
    annotationProcessor "android.arch.persistence.room:compiler:1.0.0"
    // Paging
    implementation 'android.arch.paging:runtime:1.0.0-alpha5'
    // Java8 support for Lifecycles
    implementation 'android.arch.lifecycle:common-java8:1.1.0'
    // Test helpers for LiveData
    testImplementation 'android.arch.core:core-testing:1.1.0'
    // Test helpers for Room
    testImplementation 'android.arch.persistence.room:testing:1.0.0'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'
    testCompile "org.jetbrains.kotlin:kotlin-stdlib-jre7:${kotlinVersion}"
    testCompile "org.robolectric:robolectric:${robolectricVersion}"
    testCompile "junit:junit:${junitVersion}"
    testCompile "com.google.truth:truth:${truthVersion}"
    testCompile "org.mockito:mockito-core:${mockitoVersion}"
    testCompile 'org.jdom:jdom2:2.0.6'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    implementation "com.android.support:appcompat-v7:${androidSupportLibraryVersion}"
    implementation "com.android.support:design:${androidSupportLibraryVersion}"
    implementation "com.android.support:support-annotations:${androidSupportLibraryVersion}"
    implementation "com.android.support:cardview-v7:${androidSupportLibraryVersion}"
    implementation "com.android.support:recyclerview-v7:${androidSupportLibraryVersion}"
}

android {
    compileSdkVersion androidCompileSdkVersion.toInteger()
    buildToolsVersion androidBuildToolsVersion

    defaultConfig {
        applicationId "com.fsck.k9"
        testApplicationId "com.fsck.k9.tests"

        versionCode 25000
        versionName '5.500-SNAPSHOT'

        minSdkVersion 15
        targetSdkVersion 22

        generatedDensities = ['mdpi', 'hdpi', 'xhdpi']

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release
    }

    buildTypes {
        release {
            if (project.hasProperty('storeFile')) {
                signingConfig signingConfigs.release
            }

            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            buildConfigField "boolean", "DEVELOPER_MODE", "false"
        }

        debug {
            applicationIdSuffix ".debug"
            testCoverageEnabled rootProject.testCoverage

            buildConfigField "boolean", "DEVELOPER_MODE", "true"
        }
    }

    // Do not abort build if lint finds errors
    lintOptions {
        abortOnError false
        lintConfig file("$rootProject.projectDir/config/lint/lint.xml")
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/README'
        exclude 'LICENSE.txt'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    buildToolsVersion '26.0.2'
}

if (project.hasProperty('keyAlias')) {
    android.signingConfigs.release.keyAlias = keyAlias
}
if (project.hasProperty('keyPassword')) {
    android.signingConfigs.release.keyPassword = keyPassword
}
if (project.hasProperty('storeFile')) {
    android.signingConfigs.release.storeFile = file(storeFile)
}
if (project.hasProperty('storePassword')) {
    android.signingConfigs.release.storePassword = storePassword
}

apply plugin: 'kotlin-android-extensions'